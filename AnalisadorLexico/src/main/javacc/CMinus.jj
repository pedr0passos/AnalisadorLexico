options {
    LOOKAHEAD = 2;
}

PARSER_BEGIN(SimpleParser)

package javacc;

import java.io.InputStream;
import java.util.ArrayList;
import model.ModelToken;

public class SimpleParser {
    
    public void analisar() throws ParseException {
        this.Start();
    }

    public ArrayList<ModelToken> getTokens(InputStream codigo) throws ParseException{
        var tokens = new ArrayList<ModelToken>(); 
        while (true) {
            Token token = this.getNextToken();
            if (token.kind == 0) break;
            tokens.add(new ModelToken(SimpleParserConstants.tokenImage[token.kind], token.image));
        }
        return tokens;
    }
}

PARSER_END(SimpleParser)

SKIP : { " " | "\t" | "\n" | "\r" }

TOKEN : {
    < DEFINE: "#define" >
  | < #DIGIT: ["0"-"9"] >
  | < #LETTER: ["a"-"z", "A"-"Z", "_"] >
  | < NUMBER: (<DIGIT>)+ ("." (<DIGIT>)+)? >
  | < ADD: "+" >
  | < SUB: "-" >
  | < MUL: "*" >
  | < DIV: "/" >
  | < MOD: "%" >
  | < LPAREN: "(" >
  | < RPAREN: ")" >
  | < SEMICOLON: ";" >
  | < AUTO: "auto" >
  | < STATIC: "static" >
  | < EXTERN: "extern" >
  | < CONST: "const" >
  | < VOID: "void" >
  | < CHAR: "char" >
  | < FLOAT: "float" >
  | < DOUBLE: "double" >
  | < SIGNED: "signed" >
  | < UNSIGNED: "unsigned" >
  | < SHORT: "short" >
  | < INT: "int" >
  | < LONG: "long" >
  | < RETURN: "return" >
  | < PRINTF: "printf" >
  | < SCANF: "scanf" >
  | < BREAK: "break" >
  | < IF: "if" >
  | < ELSE: "else" >
  | < OR: "||" >
  | < AND: "&&" >
  | < EQ: "==" >
  | < NEQ: "!=" >
  | < LT: "<" >
  | < LTE: "<=" >
  | < GT: ">" >
  | < GTE: ">=" >
  | < ASSIGN: "=" >
  | < PLUS_ASSIGN: "+=" >
  | < MINUS_ASSIGN: "-=" >
  | < MUL_ASSIGN: "*=" >
  | < DIV_ASSIGN: "/=" >
  | < MOD_ASSIGN: "%=" >
  | < COMMA: "," >
  | < LBRACKET: "[" >
  | < RBRACKET: "]" >
  | < LBRACE: "{" >
  | < RBRACE: "}" >
  | < DOT: "." >
  | < LITERAL: "\"" (~["\""])* "\"" >
  | < IDENTIFIER: (<LETTER>)(<LETTER> | <DIGIT>)* >
}

void Start() : {} {
    Programa() <EOF>
}

void Programa() : {} {
    (
        Declaracao() Programa()
      | <DEFINE> <IDENTIFIER> <NUMBER> Programa()
      | {} // Produção vazia para permitir fim do programa
    )
}

void Declaracao() : {} {
    Especificador() Tipo() <IDENTIFIER> Declaracao2() <SEMICOLON>
}

void Declaracao2() : {} {
    (
        <LBRACKET> <NUMBER> <RBRACKET> // Array
      | <LPAREN> ListaParametros() <RPAREN> Bloco() // Função
      | <ASSIGN> Expr() // Atribuição
      | {} // Produção vazia para declaração simples
    )
}

void Especificador() : {} {
    ( <AUTO> | <STATIC> | <EXTERN> | <CONST> )?
}

void Tipo() : {} {
    (
        <VOID> | <CHAR> | <FLOAT> | <DOUBLE>
      | <SIGNED> Inteiro()
      | <UNSIGNED> Inteiro()
      | Inteiro()
    )
}

void Inteiro() : {} {
    ( <SHORT> | <INT> | <LONG> )
}

void ListaParametros() : {} {
    ( DeclaracaoParam() ( <COMMA> DeclaracaoParam() )* )?
}

void DeclaracaoParam() : {} {
    Tipo() <IDENTIFIER> DeclaracaoParam2()
}

void DeclaracaoParam2() : {} {
    ( <LBRACKET> <NUMBER> <RBRACKET> )?
}

void Bloco() : {} {
    <LBRACE> ( Instrucoes() )* <RBRACE>
}

void Instrucoes() : {} {
    (
        <IDENTIFIER> Expressao() <SEMICOLON>
      | <RETURN> Expr() <SEMICOLON>
      | <PRINTF> <LPAREN> Expr() <RPAREN> <SEMICOLON>
      | <SCANF> <LPAREN> <IDENTIFIER> <RPAREN> <SEMICOLON>
      | <BREAK> <SEMICOLON>
      | <IF> <LPAREN> Expr() <RPAREN> Instrucoes() ( <ELSE> Instrucoes() )?
      | Bloco()
      | Declaracao()
    )
}

void Expressao() : {} {
    Atribuicao()
}

void Atribuicao() : {} {
    Expr() ( OperadorAtrib() Expr() )?
}

void OperadorAtrib() : {} {
    ( <ASSIGN> | <MUL_ASSIGN> | <DIV_ASSIGN> | <MOD_ASSIGN> | <PLUS_ASSIGN> | <MINUS_ASSIGN> )
}

void Expr() : {} {
    ExprOr()
}

void ExprOr() : {} {
    ExprAnd() ( <OR> ExprAnd() )*
}

void ExprAnd() : {} {
    ExprEqual() ( <AND> ExprEqual() )*
}

void ExprEqual() : {} {
    ExprRelacional() ( ( <EQ> | <NEQ> ) ExprRelacional() )*
}

void ExprRelacional() : {} {
    ExprAditiva() ( ( <LT> | <LTE> | <GT> | <GTE> ) ExprAditiva() )*
}

void ExprAditiva() : {} {
    ExprMultiplicativa() ( ( <ADD> | <SUB> ) ExprMultiplicativa() )*
}

void ExprMultiplicativa() : {} {
    ExprUnaria() ( ( <MUL> | <DIV> | <MOD> ) ExprUnaria() )*
}

void ExprUnaria() : {} {
    ( <ADD> | <SUB> )? ExprPrimaria()
}

void ExprPrimaria() : {} {
    (
        <IDENTIFIER> ( <LBRACKET> Expr() <RBRACKET> | <LPAREN> ExprLista() <RPAREN> )?
      | <NUMBER>
      | <LITERAL>
      | <LPAREN> Expr() <RPAREN>
    )
}

void ExprLista() : {} {
    ( Expr() ( <COMMA> Expr() )* )?
}